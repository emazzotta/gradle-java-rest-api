apply plugin: 'groovy'

buildscript {
    repositories {
        jcenter()
        maven { url "https://repo.grails.org/grails/core" }
    }

    dependencies {
        classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.13.1"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    }

}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

version = '0.9.0'

allprojects {

    apply from: "${rootDir}/gradle/java.gradle"

    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: "com.github.hierynomus.license"

    license {
        header = file("${rootDir}/LICENSE-SHORT")

        ext.year = Calendar.getInstance().get(Calendar.YEAR)
        ext.name = 'Silvio Wangler'
        ext.email = 'silvio.wangler@gmail.com'

        excludes(["**/*.json", "**/*.java.txt"])
    }
}


dependencies {
    compile localGroovy()
    compile gradleApi()
    testCompile gradleTestKit()

    testCompile("org.spockframework:spock-core:${spockVersion}") {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }

    compile "com.squareup:javapoet:${javaPoetVersion}"
    compile "com.google.guava:guava:${guavaVersion}"
    compile "com.google.code.gson:gson:${gsonVersion}"

    compile project(':json-model')
}



bintray {
    user = project.hasProperty('bintrayUser') ? bintrayUser : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? bintrayKey : System.getenv('BINTRAY_KEY')

    pkg {
        repo = 'gradle-plugins'
        name = 'gradle-java-rest-api'
        userOrg = 'saw303'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/saw303/gradle-java-rest-api.git'

        version {
            name = project.version
        }
    }

    publications = ['MyPublication']
}

def pomConfig = {
    developers {
        developer {
            id "saw303"
            name "Silvio Wangler"
            email "silvio.wangler@gmail.com"
        }
    }

    scm {
        url "https://github.com/saw303/gradle-java-rest-api.git"
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'ch.silviowangler.gradle'
            artifactId 'gradle-java-rest-api'
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Gradle plugin to generate REST APIs for JAX-RS or Spring-Boot projects')
                root.appendNode('name', 'Silvio Wangler')
                root.appendNode('url', 'https://github.com/saw303/gradle-java-rest-api')

                def l = root.appendNode('licenses').appendNode('license')
                l.appendNode('name', 'MIT')
                l.appendNode('url', 'https://en.wikipedia.org/wiki/MIT_License')
                l.appendNode('distribution', 'repo')

                root.children().last() + pomConfig
            }
        }
    }
}